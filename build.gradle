// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: "com.github.ben-manes.versions"

buildscript {

    ext.compileSdkVersionV = 28
    ext.targetSdkVersionV = 28
    ext.minSdkVersionV = 21
    ext.versionCode = 1
    ext.versionName = "0.1"

    ext.android_plugin_version = '3.1.4'
    ext.assert_j_version = "3.11.1"
    ext.constraint_layout_version = "1.1.3"
    ext.dagger_version = "2.18"
    ext.dexmaker_version = "1.4"
    ext.espresso_version = "3.0.2"
    ext.jsoup_version = "1.11.3"
    ext.kotlin_version = '1.2.71'
    ext.lifecycle_version = "1.1.1"
    ext.mockito_version = "1.6.0"
    ext.okhttp_version = "3.11.0"
    ext.picasso_version = "2.71828"
    ext.robolectric_version = "3.8"
    ext.rx_android_version = "2.1.0"
    ext.rx_java_version = "2.2.2"
    ext.room_version = "1.1.1"
    ext.support_lib_version = "28.0.0"
    ext.test_support_version = "1.0.2"
    ext.truth_version = "0.42"
    ext.truth_versionkotlin = "0.40"
    ext.uiautomator_version = "2.1.3"

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.20.0"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

task unitTest(dependsOn: ['app:testDebugUnitTest',
                          'common:testDebugUnitTest',
                          'deviceinfo:testDebugUnitTest',
                          'launcher:testDebugUnitTest']) {
    group = 'tests'
    description = "Run all unit tests"
}

task uiTest(dependsOn: ['app:connectedAndroidTest',
                        'common:connectedAndroidTest',
                        'deviceinfo:connectedAndroidTest',
                        'launcher:connectedAndroidTest']) {
    group = 'tests'
    description = "Run all instrumentation tests"
}
